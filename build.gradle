plugins {
	id 'fabric-loom' version '1.4.+'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${mod_name}-${minecraft_version}"

version = project.mod_version
group = project.group

loom {
    runs {
		datagen {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=${mod_id}"

			runDir "build/datagen"
		}
    }

	accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

repositories {
	maven {
		name = 'Parchment'
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroupAndSubgroups "org.parchmentmc"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroupAndSubgroups "maven.modrinth"
		}
	}
	maven {
		name = "Architectury"
		url "https://maven.architectury.dev/"
		content {
			includeGroupAndSubgroups "me.shedaniel"
			includeGroupAndSubgroups "dev.architectury"
		}
	}
	maven {
		name = "Mod Menu and Trinkets"
		url "https://maven.terraformersmc.com/releases/"
		content {
			includeGroupAndSubgroups "com.terraformersmc"
			includeGroupAndSubgroups "dev.emi"
		}
	}
	maven {
		name 'SpruceUI'
		url 'https://maven.gegy.dev'
		content {
			includeGroupAndSubgroups "dev.lambdaurora"
		}
	}
	maven {
		name = "Trinkets and Cardinal Components API"
		url = 'https://maven.ladysnake.org/releases'
		content {
			includeGroupAndSubgroups "dev.emi"
			includeGroupAndSubgroups 'dev.onyxstudios.cardinal-components-api'
		}
	}
	maven {
		name = "TslatEffectsLib"
		url = "https://dl.cloudsmith.io/public/tslat/tel/maven/"
		content {
			includeGroupAndSubgroups "net.tslat.effectslib"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"

	mappings loom.layered() {
//		parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
		parchment("org.parchmentmc.data:parchment-1.20.3:${parchment_version}@zip")
		officialMojangMappings()
	}

	implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "dev.emi:trinkets:${trinkets_version}"
	modImplementation "net.tslat.effectslib:TslatEffectsLib-fabric-${minecraft_version}:${tslateffectslib_version}"

	// For trinkets
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}"

	modRuntimeOnly "dev.lambdaurora:spruceui:${spruceui_version}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
	modRuntimeOnly "maven.modrinth:huge-structure-blocks:${hugestructureblocks_version}"
	modRuntimeOnly "maven.modrinth:worldedit:${worldedit_id}"
	modRuntimeOnly "maven.modrinth:midnightlib:${midnightlib_version}-fabric"
	modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"
	modRuntimeOnly "maven.modrinth:jade:${jade_version}"
}

processResources {
	inputs.property "version", mod_version

	filesMatching("fabric.mod.json") {
		expand "version": mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}